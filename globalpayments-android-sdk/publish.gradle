apply plugin: "maven-publish"
apply plugin: 'signing'

// Artifact parameters
def sdkGroupId = 'com.heartlandpaymentsystems'
def sdkArtifactId = 'globalpayments-android-sdk'
def sdkVersion = '1.1.11'

// publish.properties should contain signing properties and maven central credentials
File publishPropertiesFile = file('publish.properties')
Properties publishProperties = new Properties()
new FileInputStream(publishPropertiesFile).withCloseable { is -> publishProperties.load(is) }

ext["signing.keyId"] = publishProperties["signing.keyId"]
ext["signing.password"] = publishProperties["signing.password"]
ext["signing.secretKeyRingFile"] = publishProperties["signing.secretKeyRingFile"]
ext["mavenCentralRepoUsername"] = publishProperties["mavenCentralRepoUsername"]
ext["mavenCentralRepoPassword"] = publishProperties["mavenCentralRepoPassword"]

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact androidJavadocsJar
                artifact androidSourcesJar

                groupId = sdkGroupId
                artifactId = sdkArtifactId
                version = sdkVersion

                pom {
                    name = sdkArtifactId
                    description = 'The official Android SDK for Global Payments API.'
                    url = 'https://developer.globalpay.com'

                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'globalpayments'
                            name = 'Global Payments'
                            email = 'developers@globalpay.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/globalpayments/android-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com/globalpayments/android-sdk.git'
                        url = 'https://github.com/globalpayments/android-sdk'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "MavenCentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username mavenCentralRepoUsername
                    password mavenCentralRepoPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
