apply plugin: 'com.android.application'
apply plugin: "kotlin-android"

Properties configProperties = new Properties()
if (project.file("local.properties").exists()) {
    configProperties.load(project.file("local.properties").newDataInputStream())
} else {
    configProperties.load(project.file("configuration.properties").newDataInputStream())
}
def appId = configProperties.getProperty("appId")
def appKey = configProperties.getProperty("appKey")
def serviceUrl = configProperties.getProperty("serviceUrl")
def challengeNotificationUrl = configProperties.getProperty("challengeNotificationUrl")
def methodNotificationUrl = configProperties.getProperty("methodNotificationUrl")
def apiVersion = configProperties.getProperty("apiVersion")
def tokenSecondsToExpire = configProperties.getProperty("tokenSecondsToExpire")
def tokenIntervalToExpire = configProperties.getProperty("tokenIntervalToExpire")
def country = configProperties.getProperty("country")
def channel = configProperties.getProperty("channel")
def environment = configProperties.getProperty("environment")


android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.globalpayments.android.sdk.sample"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.1.15"

        buildConfigField "String", "APP_ID", "\"$appId\""
        buildConfigField "String", "APP_KEY", "\"$appKey\""
        buildConfigField "String", "SERVICE_URL", "\"$serviceUrl\""
        buildConfigField "String", "CHALLENGE_NOTIFICATION_URL", "\"$challengeNotificationUrl\""
        buildConfigField "String", "METHOD_NOTIFICATION_URL", "\"$methodNotificationUrl\""
        buildConfigField "String", "API_VERSION", "\"$apiVersion\""
        buildConfigField "Integer", "TOKEN_SECONDS_TO_EXPIRE", "$tokenSecondsToExpire"
        buildConfigField "String", "TOKEN_INTERVAL_TO_EXPIRE", "\"$tokenIntervalToExpire\""
        buildConfigField "String", "COUNTRY", "\"$country\""
        buildConfigField "String", "CHANNEL", "\"$channel\""
        buildConfigField "String", "ENVIRONMENT", "\"$environment\""

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation files("libs/3ds-sdk.aar")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // Google
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'

    // globalpayments-android-sdk
    implementation project(':globalpayments-android-sdk')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'

    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.bouncycastle:bcprov-ext-jdk18on:1.71'
    implementation 'org.bitbucket.b_c:jose4j:0.7.12'

    //google pay
    implementation "com.google.android.gms:play-services-wallet:19.1.0"

    implementation "androidx.browser:browser:1.4.0"
}
